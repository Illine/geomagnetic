plugins {
    id "java"
    id "idea"
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "org.springframework.boot" version "2.1.5.RELEASE"
    id 'org.liquibase.gradle' version '2.0.1'
}

group = "net.c7j.weather.geomagnetic"
version = "0.0.1-SNAPSHOT"
targetCompatibility = '11'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "Greenwich.SR1")
    set("liquibaseCoreVersion", "3.6.3")
    set("modelMapperVersion", "2.3.4")
    set("p6spyVersion", "3.8.2")
    set("httpComponentsVersion", "4.5.9")
    set("lombokVersion", "1.18.8")
    set("postgresVersion", "42.2.5")
    set("groovyVersion", "2.5.5")
    set("liquibaseDslVersion", "2.0.1")
    set("logbackVersion", "1.2.3")
    set("junitVersion", "5.4.2")
    set("h2Version", "1.4.199")
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    implementation "org.modelmapper:modelmapper:${modelMapperVersion}"
    implementation "p6spy:p6spy:${p6spyVersion}"
    implementation "org.apache.httpcomponents:httpclient:${httpComponentsVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    runtimeOnly "org.postgresql:postgresql:${postgresVersion}"
    runtimeOnly "org.codehaus.groovy:groovy-all:${groovyVersion}"

    liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:${liquibaseDslVersion}"
    liquibaseRuntime "ch.qos.logback:logback-classic:${logbackVersion}"
    liquibaseRuntime "org.postgresql:postgresql:${postgresVersion}"

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "com.h2database:h2:${h2Version}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

liquibase {
    activities {
        main {
            changeLogFile "${projectDir}/src/main/resources/liquibase/changelog.yaml"
            outputFile "${buildDir}/liquibase/sql-migration.sql"
            url "jdbc:postgresql://localhost:5000/geomagnetic"
            username "liquibase"
            password "liquibase"
            defaultSchemaName "geomagnetic"
        }
    }
}

test {
    useJUnitPlatform {
        includeTags "UnitTest"
        includeTags "IntegrationTest"
        excludeTags "SystemTest"
    }
}
